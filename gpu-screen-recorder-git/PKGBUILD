# Maintainer: dec05eba <dec05eba@protonmail.com>

pkgname=gpu-screen-recorder-git
pkgver=r618.3e2e244
pkgrel=1
pkgdesc='A shadowplay-like screen recorder for Linux. The fastest screen recorder for Linux'
arch=('x86_64')
url="https://git.dec05eba.com/gpu-screen-recorder"
license=('GPL3')
makedepends=('meson')
depends=('ffmpeg' 'libglvnd' 'libxcomposite' 'libxrandr' 'libxfixes' 'libx11' 'libpulse' 'libva' 'libdrm' 'libcap' 'wayland')
optdepends=(
  'nvidia-utils: Required to record your screen on NVIDIA'
  'libxnvctrl: Required when using the -oc option to overclock the NVIDIA GPU to workaround NVIDIA p2 state bug'
  'mesa: Required to record your screen on AMD/Intel'
  'libva-mesa-driver: Required to record your screen on AMD'
  'libva-intel-driver: Required to record your screen on Intel G45 and HD Graphics family'
  'intel-media-driver: Required to record your screen on Intel Broadwell+ iGPUs'
)
provides=('gpu-screen-recorder')
conflicts=('gpu-screen-recorder')
source=("${pkgname}-${pkgver}.tar.gz::https://dec05eba.com/snapshot/gpu-screen-recorder.git.${pkgver}.tar.gz")
sha512sums=('e9b45e4f01a50da5d4cebf94c06e1b4a7aaaee8be649ac8dfd6527112ea488accd3f5607917ebc2d4765d65a2df70350d09612d1674440c532d1c55e32876262')
install="${pkgname}.install" # setcap cap_sys_admin (gsr-kms-server), setcap cap_sys_nice (gpu-screen-recorder)

build() {
  cd "$srcdir"
  arch-meson build -Dsystemd=true
  meson compile -v -C build
}

package() {
  cd "$srcdir"
  meson install -C build --destdir "$pkgdir"
}

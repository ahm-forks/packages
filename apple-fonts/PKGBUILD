# Maintainer:  Alessandro Marchioro <marciozgaming@gmail.com>
# Co-Maintainer: Cp Dong <cp-dong at outlook dot com>
pkgname=apple-fonts
pkgver=1.1
pkgrel=1
pkgdesc='Apple San Francisco, New York fonts, directly from Apple official source'
arch=(any)
url='https://developer.apple.com/fonts/'
license=('custom')
provides=('ttf-font')
depends=()
makedepends=(p7zip)
source=('https://devimages-cdn.apple.com/design/resources/download/SF-Pro.dmg'
  'https://devimages-cdn.apple.com/design/resources/download/SF-Compact.dmg'
  'https://devimages-cdn.apple.com/design/resources/download/SF-Mono.dmg'
  'https://devimages-cdn.apple.com/design/resources/download/NY.dmg'
  'LICENSE.THE-APPLE-SF-PRO-FONT'
  'LICENSE.THE-APPLE-SF-COMPACT-FONT'
  'LICENSE.THE-APPLE-SF-MONO-FONT'
  'LICENSE.THE-APPLE-NEW-YORK-FONT')
sha256sums=('07cc658c102a3a84455ef48e90e2830d679851e6ed32640b27c945d398859d79'
  '2f8a0b437e04a70d7fc0b7a153db1743051efcb69e2a31d1c50005eaeda97d3a'
  '51aaf1d5328eee0e725415c0c7a6248b4eb9af3ff046e6221cfcf38ba7134e5f'
  'c98caa928c76c7d9d0f38da56970aa0375303a9506c887d4ba9ad7f7be4eb0b0'
  '18273048eee4851c929531c954a5c36bbd897404e49c69309a3fc452973c3885'
  'a3c5661529d654e92e7310fc2572eae7f6a165db54be918e2bee35afb39159f1'
  '079512256688e048803fedeb087eaaf8a3109448f160c6f84c6561f4eb763296'
  '5579b2b33d529bd51efab1f378e5c600aafd8fe1481f3c28e5d914742c801ba2')
noextract=("${source[@]##*/}")

prepare() {
  cd "$srcdir"
  mkdir -p fonts src
  for archive in *.dmg; do
    7z e "$archive" -y -osrc/
    cd src/
    7z x *.pkg -y
    7z x 'Payload~'
    mv Library/Fonts/* "$srcdir/fonts/"
    cd "$srcdir"
    rm -rf src/{*,.*}
  done
  rmdir src/
}

package() {
  install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname" \
    LICENSE.THE-APPLE-SF-PRO-FONT LICENSE.THE-APPLE-SF-COMPACT-FONT \
    LICENSE.THE-APPLE-SF-MONO-FONT LICENSE.THE-APPLE-NEW-YORK-FONT

  install -Dm644 -t "$pkgdir/usr/share/fonts/$pkgname" "$srcdir/fonts"/*
}
